load(
    "@rules_haskell//haskell:defs.bzl",
    "haskell_library",
)
load("@config_settings//:info.bzl", "ghc_version")

# Note cat-tokens
#
# This is a script to concatenate tokens after CPP preprocessing.
#
# On OSX we used to rely on cpphs to concatenate tokens in
# definitions like
#
# define GET_FIELD(name, hs_rettype, c_rettype) \
#   get/**/name/**/Field :: Coercible o (J a) => o -> JFieldID -> IO hs_rettype;
#
# The C preprocessor in OSX would otherwise replace the
# comments with whitespaces.
#
# Using cpphs, however, required a couple of hacks to workaround
# https://github.com/haskell/cabal/issues/4278
# https://gitlab.haskell.org/ghc/ghc/-/issues/17185
#
# And moreover, when using rules_haskell, ghc passes response files to cpphs,
# which are unsupported.
# https://github.com/tweag/rules_haskell/pull/836
#
# Therefore, we currently resolve concatenation with a custom
# preprocessor to avoid the pile of hacks. The concatenator operator is ####.
#
# define GET_FIELD(name, hs_rettype, c_rettype) \
#   get####name####Field :: Coercible o (J a) => o -> JFieldID -> IO hs_rettype;
# 
genrule(
    name = "cat-tokens-sh",
    srcs = [],
    executable = 1,
    outs = ["cat-tokens.sh"],
    tools = ["@sed//:bin"],
    cmd = """
cat > $@ <<END
#!/bin/sh
( echo "{-# LINE 1 \\"\\$$1\\" #-}" ; $(location @sed//:bin) 's/####//g' \\$$2) > \\$$3
exit \\$$?
END
    """,
)

haskell_library(
    name = "jni",
    srcs = glob(['src/common/**/*.hs', 'src/common/**/*.hsc']) +
           (glob(['src/linear-types/**/*.hs']) if ghc_version == "9.0.1" else []),
    extra_srcs = ["//jni:cat-tokens-sh"],
    compiler_flags = ["-F", "-pgmF$(location cat-tokens-sh)"],
    deps = [
        "@openjdk//:lib",
        "@stackage//:async",
        "@stackage//:base",
        "@stackage//:bytestring",
        "@stackage//:choice",
        "@stackage//:containers",
        "@stackage//:constraints",
        "@stackage//:deepseq",
        "@stackage//:inline-c",
        "@stackage//:stm",
        "@stackage//:text",
    ] + [
        "@stackage//:linear-base" if ghc_version == "9.0.1" else
        "@stackage//:singletons"
    ],
    tools = ["@sed//:bin"],
    visibility = ["//visibility:public"],
)
